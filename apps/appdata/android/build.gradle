// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    // 🔧 AGP 버전 명시 (Gradle 8.13과 호환)
    classpath('com.android.tools.build:gradle:8.7.3')
    classpath('com.facebook.react:react-native-gradle-plugin')
    classpath('org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.20')
  }
}

def monorepoRoot = rootDir.getAbsoluteFile().getParentFile().getParentFile().getParentFile()

def reactNativeAndroidDir = new File(
  providers.exec {
    workingDir(monorepoRoot)
    commandLine("node", "--print", "require.resolve('react-native/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android"
)

allprojects {
  repositories {
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url(reactNativeAndroidDir)
    }

    google()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
  }

  // Kotlin 버전을 2.1.20으로 통일
  configurations.all {
    resolutionStrategy {
      force 'org.jetbrains.kotlin:kotlin-stdlib:2.1.20'
      force 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.1.20'
      force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.1.20'
      force 'org.jetbrains.kotlin:kotlin-stdlib-common:2.1.20'
    }
  }
}

apply plugin: "expo-root-project"
apply plugin: "com.facebook.react.rootproject"
